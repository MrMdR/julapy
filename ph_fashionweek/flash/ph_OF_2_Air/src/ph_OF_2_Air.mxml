<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute" width="480" height="640" windowComplete="init()">

    <mx:Script>
        <![CDATA[
        	import com.holler.utils.MathUtil;
        	import com.julapy.vo.TrackerVO;
        	import com.julapy.model.OFDataModel;
        	import com.julapy.core.StageSizeEvent;
        	import com.julapy.core.StageSize;
        	import com.julapy.ui.Btn01;
        	import com.julapy.net.SocketOFEvent;
        	import com.julapy.net.SocketOF;
		    import flash.display.Sprite;

			private var model		: OFDataModel = new OFDataModel();

			private var socket 		: SocketOF;

			private var container	: Sprite;
			private var info		: TextField;
			private var fsBtn		: Btn01;

			private function init ():void
			{
				StageSize.getInstance().stage = stage;
				StageSize.getInstance().addEventListener( StageSizeEvent.RESIZE, stageResizeHandler );

				socket = new SocketOF();
				socket.addEventListener( SocketOFEvent.CONNECTED,			socketHandler );
				socket.addEventListener( SocketOFEvent.DISCONNECTED,		socketHandler );
				socket.addEventListener( SocketOFEvent.TRYING_TO_CONNECT,	socketHandler );
				socket.addEventListener( SocketOFEvent.DATA_RECEIVED,		socketHandler );

				stage.addChild( container = new Sprite() );

				info = new TextField();
				container.addChild( info );

				fsBtn = new Btn01( );
				fsBtn.addEventListener( MouseEvent.MOUSE_DOWN, fsBtnHandler );
				container.addChild( fsBtn );

				container.addEventListener( Event.ENTER_FRAME, enterFrameHandler );

				resize( StageSize.getInstance().stageWidth, StageSize.getInstance().stageHeight );
			}

			private function socketHandler ( e : SocketOFEvent ):void
			{
				if( e.type == SocketOFEvent.CONNECTED )
				{
					info.appendText( "\n" + "connected!" );
				}

				if( e.type == SocketOFEvent.DISCONNECTED )
				{
					info.text = "disconnected.";
				}

				if( e.type == SocketOFEvent.TRYING_TO_CONNECT )
				{
					info.appendText( "\n" + "no luck, waiting :( " );
				}

				if( e.type == SocketOFEvent.DATA_RECEIVED )
				{
					model.ofStringData = e.dataString;

					info.text  = "\n" + "connected!";
					info.appendText( "\n" + e.dataString );
				}
			}

			private function fsBtnHandler ( e : MouseEvent ):void
			{
				if( stage.displayState != StageDisplayState.FULL_SCREEN_INTERACTIVE )
				{
					stage.displayState = StageDisplayState.FULL_SCREEN_INTERACTIVE;
				}
				else
				{
					stage.displayState = StageDisplayState.NORMAL;
				}
			}

			private function stageResizeHandler ( e : StageSizeEvent ):void
			{
				resize( e.stageWidth, e.stageHeight );
			}

			private function resize ( stageWidth : int, stageHeight : int ):void
			{
				info.width	= stageWidth;
				info.height = stageHeight;

				fsBtn.x	= stageWidth - 50;
				fsBtn.y	= 10;
			}

			private function enterFrameHandler ( e : Event ):void
			{
				var trackerVOs : Array;
				trackerVOs = model.trackerVOs;

				var colours : Array;
				colours = [ 0xFF0000, 0x00FF00, 0x0000FF ];

				var stageWidth 	: int;
				var stageHeight	: int;
				stageWidth	= StageSize.getInstance().stageWidth;
				stageHeight	= StageSize.getInstance().stageHeight;

				var r : Rectangle;
				r = new Rectangle();
				r.x			= 0;
				r.y			= 0;
				r.width		= stageWidth;
				r.height	= (int)( stageWidth * ( 3 / 4 ) );

				container.graphics.clear();
				container.graphics.beginFill( 0x333333 );
				container.graphics.drawRect( r.x, r.y, r.width, r.height );
				container.graphics.endFill();

				for( var i:int=0; i<trackerVOs.length; i++ )
				{
					var j : int;
					j = Math.min( i, colours.length - 1 );

					var trackerVO : TrackerVO;
					trackerVO = trackerVOs[ i ];

					//-- rect.

					var rectName : String;
					rectName = "rect_" + i.toString();

					var rectMc : Sprite;
					rectMc = container.getChildByName( rectName ) as Sprite;

					if( !rectMc )
					{
						rectMc = new Sprite();
						rectMc.name = rectName;
						container.addChild( rectMc );
					}

					rectMc.graphics.clear();

					if( !trackerVO.active )
						continue;

					rectMc.x = trackerVO.rect.x * r.width;
					rectMc.y = trackerVO.rect.y * r.height;

					rectMc.rotation = trackerVO.angle * 360;

					rectMc.graphics.lineStyle( 2, colours[ j ], 1.0 );
					rectMc.graphics.drawRect
					(
						-trackerVO.rect.width  * 0.5 * r.width,
						-trackerVO.rect.height * 0.5 * r.height,
						trackerVO.rect.width   * r.width,
						trackerVO.rect.height  * r.height
					);
				}
			}

        ]]>
    </mx:Script>
</mx:WindowedApplication>